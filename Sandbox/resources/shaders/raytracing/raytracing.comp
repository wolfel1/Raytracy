#version 450 core

struct Node {
	vec3 min_corner;
	uint left_child_index;
	vec3 max_corner;
	uint right_child_index;
	bool has_triangle;
	uint lookup_index;
	uint triangle_count;
};

struct Mesh {
	uint material_index;
};

struct Triangle {
	uint vertex_indices[3];
	uint mesh_index;
};

struct Vertex {
	vec3 position;
	vec3 normal;
	vec4 color;
	vec2 tex_coords;
};

struct DirectionalLight {
	vec3 color;
	vec3 direction;
	float strength;
};

struct Material {
	vec4 color;
	vec3 specular;
	float shininess;
};

struct Camera {
	vec3 position;
	vec3 direction;
	vec3 up;
	vec3 right;
};

struct Ray {
	vec3 origin;
	vec3 direction;
};

struct Hit {
	vec4 color;
	float distance;
	vec3 point;
	vec3 normal;
	uint triangle_index;
};

const float infinity = 1.0 / 0.0;
const float epsilon = 0.0001;

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(rgba32f, binding = 0) uniform image2D imgOutput;

layout(std140, binding = 0) uniform SceneData {
    mat4 inverse_view;
    mat4 inverse_projection;
    vec3 camera_position;
	int samples;
	vec3 camera_direction;
	int max_depth;
};

layout(std430, binding = 0) readonly buffer Triangles {
    Triangle triangles[];
};

layout(std430, binding = 1) readonly buffer Vertices {
    Vertex vertices[];
};

layout(std430, binding = 2) readonly buffer BoundingVolumeHierarchie {
    Node nodes[];
};

layout(std430, binding = 3) readonly buffer TriangleIndices {
    uint triangle_indices[];
};

layout(std430, binding = 4) readonly buffer Lights {
    DirectionalLight lights[];
};

layout(std430, binding = 5) readonly buffer Materials {
    Material materials[];
};

layout(std430, binding = 6) readonly buffer Meshes {
    Mesh meshes[];
};

uniform samplerCube skybox;

vec4 computePixelColor(Ray ray);
bool trace(in Ray ray, float minimum, float maximum, inout Hit hit);
bool hitTriangle(in Ray ray, float minimum, float maximum, inout Hit hit, const Triangle triangle);
float hitBoundingBox(in Ray ray, in Node node);
vec3 calculate_barycentric(in vec3 point, in vec3 v0, in vec3 v1, in vec3 v2);
vec3 interpolate_normal(in vec3 barycentric, in vec3 n0, in vec3 n1, in vec3 n2);

void main() {
    vec4 value = vec4(0.0, 0.0, 0.0, 1.0);
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 image_size = imageSize(imgOutput);

    if (texelCoord.x >= image_size.x || texelCoord.y >= image_size.y) {
        return;
    }

    vec3 direction = normalize(camera_direction);
    vec2 coord = vec2(float(texelCoord.x) / float(image_size.x), float(texelCoord.y) / float(image_size.y));
    coord = coord * 2.0 - 1.0; // -1 -> 1
    vec4 target = inverse_projection * vec4(coord.x, coord.y, 1, 1);
    vec3 ray_direction = vec3(inverse_view * vec4(normalize(vec3(target) / target.w), 0));

    vec4 accumulated_color = vec4(0.0);
    for (int test = 0; test < samples; ++test) {
        Ray ray;
        ray.origin = camera_position;

        ray.direction = ray_direction;
        accumulated_color += computePixelColor(ray);
    }
    accumulated_color /= float(samples);
    accumulated_color = clamp(accumulated_color, vec4(0.0), vec4(1.0));
        
    imageStore(imgOutput, texelCoord, accumulated_color);
}



vec4 computePixelColor(Ray ray) {
    Ray current_ray = ray;
    vec4 current_attenuation = vec4(1.0);
    for (int i = 0; i < max_depth; i++) {
        Hit hit;
        if (trace(current_ray, 0.001, infinity, hit)) {
			Triangle triangle = triangles[hit.triangle_index];
			Mesh mesh = meshes[triangle.mesh_index];
			Material material = materials[mesh.material_index];

			Vertex v0 = vertices[triangle.vertex_indices[0]];
			Vertex v1 = vertices[triangle.vertex_indices[1]];
			Vertex v2 = vertices[triangle.vertex_indices[2]];
			vec3 barycentric = calculate_barycentric(hit.point, v0.position, v1.position, v2.position);
			vec3 shading_normal = interpolate_normal(barycentric, v0.normal, v1.normal, v2.normal);

            //vec3 scatter_direction = reflect(current_ray.direction, shading_normal);

			for (int j = 0; j < 1; j++) {
				DirectionalLight light = lights[j];
				vec3 lightDirection = normalize(-light.direction);
				vec3 viewDirection = normalize(-current_ray.direction);

				vec3 ambient = 0.1 * material.color.rgb;

				float diff = max(dot(shading_normal, lightDirection), 0.0);

				vec3 halfwayDir = normalize(lightDirection + viewDirection);
				float spec = pow(max(dot(shading_normal, halfwayDir), 0.0), material.shininess);

				vec3 scatteredLight = ambient + light.color * diff;
				vec3 reflectedLight = light.color * spec * light.strength;

				vec3 result = min(material.color.rgb * scatteredLight + reflectedLight, vec3(1.0));
				current_attenuation = current_attenuation * vec4(result, material.color.a);
			}

            current_ray.origin = hit.point;
            current_ray.direction = vec3(0);

        } else {
            vec3 unit_direction = normalize(current_ray.direction);
            vec4 color = texture(skybox, unit_direction);
            return current_attenuation * color;
        }
    }
    return vec4(0);
}

bool trace(in Ray ray, float minimum, float maximum, inout Hit hit) {
	Hit temp;
	bool hit_anything = false;
	float closest = maximum;

	Node node = nodes[0];

	int stack_index = 0;
	Node stack[16]; 

	if(hitBoundingBox(ray, node) == infinity) {
		return false;
	}
	
	while(true) {
		if(!node.has_triangle) {
			Node left_child = nodes[node.left_child_index];
			Node right_child = nodes[node.right_child_index];

			float distance_left = hitBoundingBox(ray, left_child);
			float distance_right = hitBoundingBox(ray, right_child);

			float closer_distance, remote_distance;
			Node closer_node, remote_node;
			if (distance_left > distance_right) {
				closer_distance = distance_right;
				remote_distance = distance_left;
				closer_node = right_child;
				remote_node = left_child;
			} else {
				closer_distance = distance_left;
				remote_distance = distance_right;
				closer_node = left_child;
				remote_node = right_child;
			}

			if(closer_distance > closest) {
				if (stack_index == 0) {
					return hit_anything;
				} else {
					stack_index--;
					node = stack[stack_index];
				}
			} else {
				node = closer_node;
				if (remote_distance < closest) {
					stack[stack_index] = remote_node;
					stack_index++;
				}
			}
		} else {
			for (uint i = 0; i < node.triangle_count; i++) {
				uint index = triangle_indices[node.lookup_index + i];
				Triangle triangle = triangles[index];
				if (hitTriangle(ray, minimum, closest, temp, triangle)) {
					closest = temp.distance;
					hit_anything = true;
					temp.triangle_index = index;
					hit = temp;
				}
			}

			if (stack_index == 0) {
				return hit_anything;
			} else {
				stack_index--;
				node = stack[stack_index];
			}
		}
	}
	

	return hit_anything;
}

float hitBoundingBox(in Ray ray, in Node node) {
	vec3 inverse_direction = vec3(1.0) / normalize(ray.direction + vec3(epsilon));
	vec3 t1 = (node.min_corner - ray.origin) * inverse_direction;
	vec3 t2 = (node.max_corner - ray.origin) * inverse_direction;
	vec3 t_min = min(t1, t2);
	vec3 t_max = max(t1, t2);

	float min_component = max(max(t_min.x, t_min.y), t_min.z);
	float max_component = min(min(t_max.x, t_max.y), t_max.z) ;
	
	if(min_component > max_component || max_component < 0) {
		return infinity;
	} else if (min_component < 0) {
		return max_component;
	} else {
		return min_component;
	}
}



bool hitTriangle(in Ray ray, float minimum, float maximum, inout Hit hit, const Triangle triangle) {
	Vertex v0 = vertices[triangle.vertex_indices[0]];
	Vertex v1 = vertices[triangle.vertex_indices[1]];
	Vertex v2 = vertices[triangle.vertex_indices[2]];

	vec3 edge1 = v1.position - v0.position;
	vec3 edge2 = v2.position - v0.position;
	
	vec3 normal = cross(ray.direction, edge2);
    float angle = dot(edge1, normal);

    if (angle < epsilon) {
        return false; // Ray is parallel to the triangle.
    }

    float inv_det = 1.0 / angle;
    vec3 distance_vec = ray.origin - v0.position;
    float u = inv_det * dot(distance_vec, normal);

    if (u < 0.0 || u > 1.0) {
        return false; // Intersection point is outside the triangle.
    }

    vec3 q = cross(distance_vec, edge1);
    float v = inv_det * dot(ray.direction, q);

    if (v < 0.0 || u + v > 1.0) {
        return false; // Intersection point is outside the triangle.
    }

    float t = inv_det * dot(edge2, q);

    if (t > minimum && t < maximum) {
		hit.color = v0.color;
        hit.distance = t;
        hit.point = ray.origin + ray.direction * t;

        hit.normal = normalize(cross(edge2, edge1));
        return true;
    }
        
	return false; // Intersection point is outside the ray segment.
    
}

vec3 calculate_barycentric(in vec3 point, in vec3 v0, in vec3 v1, in vec3 v2) {
    vec3 v0v1 = v1 - v0;
    vec3 v0v2 = v2 - v0;
    vec3 v0p = point - v0;
        
    float d00 = dot(v0v1, v0v1);
    float d01 = dot(v0v1, v0v2);
    float d11 = dot(v0v2, v0v2);
    float d20 = dot(v0p, v0v1);
    float d21 = dot(v0p, v0v2);
        
    float denom = d00 * d11 - d01 * d01;
    float v = (d11 * d20 - d01 * d21) / denom;
    float w = (d00 * d21 - d01 * d20) / denom;
    float u = 1.0 - v - w;
        
    return vec3(u, v, w);
}

vec3 interpolate_normal(in vec3 barycentric, in vec3 n0, in vec3 n1, in vec3 n2) {
    vec3 normal = {
        barycentric[0] * n0[0] + barycentric[1] * n1[0] + barycentric[2] * n2[0],
        barycentric[0] * n0[1] + barycentric[1] * n1[1] + barycentric[2] * n2[1],
        barycentric[0] * n0[2] + barycentric[1] * n1[2] + barycentric[2] * n2[2]
    };
    return normalize(normal);
}
